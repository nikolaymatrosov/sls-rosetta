resource "archive_file" "function_files" {
  output_path = "./function.zip"
  source_dir  = "../function"
  type        = "zip"
}

resource "yandex_lockbox_secret" "api_key" {
  name = "GigChat API Key"
}

resource "yandex_lockbox_secret_version" "api_key_version" {
  secret_id = yandex_lockbox_secret.api_key.id
  entries {
    key = "api_key"
    // value generated by a command won't be visible in Terraform state
    command {
      path = "bash"
      args = ["-c", "./api_key.sh"]
    }
  }
  
}

resource "yandex_function" "giga_function" {
  name               = "gigachat"
  user_hash          = archive_file.function_files.output_sha256
  runtime            = "python312"
  entrypoint         = "index.handler"
  memory             = "128"
  execution_timeout  = "300"
  content {
    zip_filename = archive_file.function_files.output_path
  }
  secrets {
    environment_variable = "GIGACHAT_API_KEY"
    id                   = yandex_lockbox_secret.api_key.id
    key                  = "api_key"
    version_id           = yandex_lockbox_secret_version.api_key_version.id
  }
  service_account_id = yandex_iam_service_account.lockbox.id
}

// IAM binding for making function public
resource "yandex_function_iam_binding" "test_function_binding" {
  function_id = yandex_function.giga_function.id
  role        = "functions.functionInvoker"
  members     = ["system:allUsers"]
}



